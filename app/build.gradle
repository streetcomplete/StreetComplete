apply plugin: 'com.android.application'

def MAPZEN_API_KEY = hasProperty('mapzenApiKey') ? '"' + mapzenApiKey + '"' : "null";

android {
    signingConfigs {
        release
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    testOptions {
        unitTests.returnDefaultValues = true
    }

    defaultConfig {
        applicationId "de.westnordost.streetcomplete"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 204
        versionName "2.4"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        buildConfigField "String", "MAPZEN_API_KEY", MAPZEN_API_KEY
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
    }

    lintOptions {
        abortOnError false
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    def props = new Properties()
    props.load(new FileInputStream(keystorePropertiesFile))
    android.signingConfigs.release.storeFile = file(props['storeFile'])
    android.signingConfigs.release.storePassword = props['storePassword']
    android.signingConfigs.release.keyAlias = props['keyAlias']
    android.signingConfigs.release.keyPassword = props['keyPassword']
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // debugging
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // tests (perhaps migrate to junit4 later if any features of it seem convenient or necessary)
    testCompile 'junit:junit:3.8.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.objenesis:objenesis:2.5.1'

    androidTestCompile 'com.android.support:support-annotations:26.0.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'org.objenesis:objenesis:2.5.1'
    androidTestCompile "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:1.4"

    // dependency injection
    dependencies {
        compile 'com.google.dagger:dagger:2.5'
        annotationProcessor 'com.google.dagger:dagger-compiler:2.5'
    }

    // Android stuff
    compile 'com.android.support:appcompat-v7:26.0.1'
    compile 'com.android.support:support-v13:26.0.1'
    compile 'com.android.support:support-v4:26.0.1'
    compile 'com.android.support:design:26.0.1'

    // scheduling background jobs
    compile 'com.evernote:android-job:1.1.12'

    // geometry
    compile 'com.vividsolutions:jts-core:1.14.0'

    // talking with OSM Api
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile ('de.westnordost:osmapi:1.7') {
        // it's already included in Android
        exclude group: 'net.sf.kxml', module: 'kxml2'
    }

    // widgets
    compile 'me.grantland:autofittextview:0.2.1'
    compile 'com.duolingo.open:rtl-viewpager:1.0.2'
    compile 'com.github.westnordost:viewtooltip:master-SNAPSHOT'

    // serialization
    compile 'com.esotericsoftware:kryo:4.0.1'

    // map and location
    compile 'com.mapzen.tangram:tangram:0.8.1'
    compile 'com.mapzen.android:lost:3.0.3'

    // config files
    compile 'com.esotericsoftware.yamlbeans:yamlbeans:1.12'
}

task generateMetadata(type: Exec) {
    commandLine 'python', 'generateCountryMetadata.py'
    commandLine 'python', 'exportCountryGeometriesToGeoJson.py'
}
// rather not, otherwise all contributors that want to build need to have installed python and the
// dependencies I use for these scripts.
//preBuild.dependsOn(generateMetadata)
